C  AUTHOR:  H.W. YORKE (UNIV. OBSERVATORY, GOETTINGEN)
C  DATE:    19-JUNE-1986
C  UPDATES: 14-JUL-86 HWY   ADD EXTRA BLANK TO CODED WRITE
C
C  THE FOLLOWING PACKAGE INCLUDES PROGRAMS FOR CODING AND DECODING
C  BINARY DATA INTO ASCII CHARACTERS. THIS IS USEFUL FOR TRANSFERING
C  DATA FROM ONE COMPUTER TO ANOTHER WHEN ONLY AN ASCII CONNECTION
C  EXISTS.
C
C  ENTRIES FOR USER:  NCODE , DCODE
C
C  ***   ENTRY NCODE   ***
C
C  CODES BINARY DATA INTO ASCII DATA AND WRITES IT ONTO A FILE
C
C  USAGE: CALL NCODE ( LUN , DAT , NDAT , NCHR , NASCII , IERR )
C-
      SUBROUTINE NCODE(KOUT,V,NDUM,NCHR,NASCII,IERR)
      IMPLICIT REAL*4 (A-H,O-Z),INTEGER*4(I-N)
      CHARACTER*1 LINE(76),BLANK
      DIMENSION V(*)
      DATA BLANK/' '/
C
C     AMAX1(XXX,YYY) = DMAX1(XXX,YYY)
C     AMIN1(XXX,YYY) = DMIN1(XXX,YYY)
C     ABS(XXX)       = DABS(XXX)
C
        ANUMAX=1.E38
        NDATA=NDUM
        NLINE=0
        DATMIN=V(1)
        DATMAX=V(1)
      DO 1 I=1,NDATA
        DATMIN=AMIN1(DATMIN,V(I))
        DATMAX=AMAX1(DATMAX,V(I))
 1    CONTINUE
        SCALE=NASCII
        SCALE=SCALE**NCHR
        COMIN=SCALE/ANUMAX
      IF(DATMAX-DATMIN .LT. COMIN) GOTO 20
        SCALE= (SCALE-.01) / (DATMAX-DATMIN)
        NWRD=76/NCHR
        NLINE=NDATA/NWRD
        NLAST=NDATA-NLINE*NWRD
      IF(NLAST.NE.0) NLINE=NLINE+1
 20   IERR=0
      WRITE(KOUT,102,ERR=999) NDATA,NLINE,NWRD,NCHR,NASCII,NLAST,
     &   DATMIN,SCALE
 102  FORMAT(2X,I10,I8,4I3,1P,2E20.13)
      IF(NLINE.EQ.0) GOTO 10
      IDATA=0
      DO 9 ILINE=1,NLINE
        ICHR=0
        DO 5 IWRD=1,NWRD
          IDATA=IDATA+1
          IF(IDATA.GT.NDATA) GOTO 6
          IV=(V(IDATA)-DATMIN)*SCALE
          DO 4 I=1,NCHR
            JV=IV
            IV=IV/NASCII
            KV=JV-IV*NASCII
            IF(KV.EQ.26)KV=63
            KV=KV+32
            ICHR=ICHR+1
 4          LINE(ICHR)=CHAR(KV)
 5      CONTINUE
        GOTO 8
 6      IWRD=(IWRD-1)*NCHR+1
        DO 7 I=IWRD,76
          LINE(I)=BLANK
 7      CONTINUE
 8      WRITE(KOUT,101,ERR=999) LINE
 101  FORMAT(2X,76A1)
 9    CONTINUE
 10   RETURN
 999  IERR=-9
      RETURN
      END
