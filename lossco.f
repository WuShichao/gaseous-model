          SUBROUTINE LOSSCO(XM,XR,JC)
C
       IMPLICIT REAL*8(A-H,O-Z),INTEGER(I-K,M-N),LOGICAL(L)
C
         INCLUDE 'compar.f'
C
C THIS SUBROUTINE COMPUTES THE LOSS-CONE FRACTION DOMEGA
C OF THE STELLAR MASS DENSITY WHICH LIES IN THE LOSS-CONE
C AND THE TERMS XER AND XET, WHICH YIELD THE MEAN ENERGY
C OF THE LOSS-CONE STARS AS RHO*SIGR2*XER AND RHO*SIGT2*XET
C
C THE VELOCITY DISTRIBUTION IS TAKEN INTO ACCOUNT UP TO
C THE SECOND ORDER (SHIFTED SCHWARZSCH.BOLTZMANN-DISTRIB.),
C I.E. UP TO THE MOMENTS RHO,U,SIGR,SIGT.
C
          DO 10 J=2,NJ
          IMA=J-1
          IF(R(J).GT.XR)GOTO 11
 10       CONTINUE
 11       CONTINUE
C
          CS2=DSQRT(2.D0)
*
          SIGR=DEXP((HX(I20+JC)-HX(I00+JC))/2.D0)
          SIGR2=SIGR*SIGR
          SIGT=DEXP((HX(I02+JC)-HX(I00+JC))/2.D0)
          ULC=HX(I10+JC)/CS2/SIGR
C****Derivatives of ULC divided by ULC**********************************
          D1U=1.D0/CS2/SIGR
          D2U=-C12*ULC
          D0U=C12*ULC
C****Calculate Potential Difference************************************
          IF(LS(19))THEN
          PHICEN=-GRAV*XM/XR
          ELSE
          PHICEN=PHI(IMA)+PHIBIN(IMA)
          END IF
C
          VHI=2.D0*(SIGR2+PHI(I)+PHIBIN(I)-PHICEN)
          VLC=DSQRT(VHI)*XR/DSQRT(R(I)*R(I)-XR*XR)/CS2/SIGT
C
C**DERIVATIVES OF VLC,VLCTH,VLCPH, TO I10,I20,I00,I02,IMR divided by VLC*****
        D1V=0.D0
        D2V=VLC*SIGR2/VHI
        D0V=VLC*(C12-SIGR2/VHI)
        IF(LS(13))THEN
        DTV=0.D0
        ELSE
        DTV=-C12*VLC
        END IF
C
C*****Enhanced loss cone in case of rotation****************************
          IF(LS(18))THEN
          ET=1.D0-XR/R(I)
          PHICEN=GRAV*XM/R(I)
          VHI=PHICEN*(1.D0-ET*ET*(1.D0-ELL))
          VLCTH=DSQRT(VHI)/CS2/SIGT
          VHI2=1.D0-ET*ET*(1.D0-ELL*SIN(THETA)**2)
          VLCPH=DSQRT(VHI*(1.D0-ET*ET)/VHI2)/CS2/SIGT
C
          D0VTH=C12*VLCTH
          D1VTH=0.D0
          D2VTH=0.D0
          DTVTH=-C12*VLCTH
C
          D0VPH=C12*VLCPH
          D1VPH=0.D0
          D2VPH=0.D0
          DTVPH=-C12*VLCPH
C    
          END IF
C
          F2P=2.D0/DSQRT(PI)
          EXU2=DEXP(-ULC*ULC)
          EXV2=DEXP(-VLC*VLC)
          ERARG=ULC
          IF(DABS(ERARG).LT.1.D1)THEN
          EARG=DABS(ERARG)
          ERU=DERF(EARG)
          ELSE
          ERU=1.D0-EPS*EPS
          END IF
          DERU=1.D0-ERU
          IF(1.D0-ERU.EQ.0.D0)DERU=EPS*EPS
C
          ERARG=VLC
          IF(DABS(ERARG).LT.1.D1)THEN
          EARG=DABS(ERARG)
          ERV=DERF(EARG)
          ELSE
          ERV=1.D0
          END IF
          ERV2=ERV*ERV
C
          DOMEGA=5.D-1*DERU*ERV2
C
          DODU=-5.D-1*F2P*ERV2*EXU2
          DODV=2.D0*DOMEGA/ERV*F2P*EXV2
C
          D0DOM=DODU*D0U+DODV*D0V
          D1DOM=DODU*D1U+DODV*D1V
          D2DOM=DODU*D2U+DODV*D2V
          DTDOM=         DODV*DTV
C
          DEPSR=DERU+F2P*ULC*EXU2
          DEPST=ERV-F2P*VLC*EXV2
          EPSR=5.D-1*DEPSR*ERV2
          EPST=5.D-1*DERU*ERV*DEPST
C
         DERUU=-2.D0*EPSR/DEPSR*F2P*ULC*ULC*EXU2
         DERV= 2.D0*EPSR/ERV*F2P*EXV2
         DETU=-EPST/DERU*F2P*EXU2
         DETV=EPST*F2P*EXV2*(1.D0/ERV+2.D0/DEPST*VLC*VLC)
C
          D0DER=DERUU*D0U+DERV*D0V
          D1DER=DERUU*D1U+DERV*D1V
          D2DER=DERUU*D2U+DERV*D2V
          DTDER=         DERV*DTV
C
          D0DET=DETU*D0U+DETV*D0V
          D1DET=DETU*D1U+DETV*D1V
          D2DET=DETU*D2U+DETV*D2V
          DTDET=         DETV*DTV
C
          IF(LS(18))THEN
C
          EXV2TH=DEXP(-VLCTH*VLCTH)
          EXV2PH=DEXP(-VLCPH*VLCPH)
C
          ERARG=VLCTH
          IF(DABS(ERARG).LT.1.D1)THEN
          EARG=DABS(ERARG)
          ERVTH=DERF(EARG)
          ELSE
          ERVTH=1.D0
          END IF
C
          ERARG=VLCPH
          IF(DABS(ERARG).LT.1.D1)THEN
          EARG=DABS(ERARG)
          ERVPH=DERF(EARG)
          ELSE
          ERVPH=1.D0
          END IF
C
          ERV2=ERVTH*ERVPH
C
          DOMEG1=5.D-1*DERU*ERV2
          IF(DOMEG1.LT.0.D0)THEN
          PRINT*,' I=',I,' DERU=',DERU,' ERV2=',ERV2
          STOP 
          END IF
C
          DODU=-5.D-1*F2P*ERV2*EXU2
          DODVTH=DOMEG1*F2P/ERVTH*EXV2TH
          DODVPH=DOMEG1*F2P/ERVPH*EXV2PH
C
          D0DOM1=DODU*D0U+D0VPH*DODVPH+D0VTH*DODVTH
          D1DOM1=DODU*D1U+D1VPH*DODVPH+D1VTH*DODVTH
          D2DOM1=DODU*D2U+D2VPH*DODVPH+D2VTH*DODVTH
          DTDOM1=         DTVPH*DODVPH+DTVTH*DODVTH
C
          DEPSR=DERU+F2P*ULC*EXU2
          DEPSTH=ERVTH-F2P*VLCTH*EXV2TH
          DEPSPH=ERVPH-F2P*VLCPH*EXV2PH
          DEPS=ERVTH*DEPSPH+ERVPH*DEPSTH
          EPSR1=5.D-1*DEPSR*ERV2
          EPST1=5.D-1*DERU*DEPS/2.D0
C
         DERUU=-2.D0*EPSR1/DEPSR*F2P*ULC*ULC*EXU2
         DERVTH=EPSR1*F2P*EXV2TH/ERVTH
         DERVPH=EPSR1*F2P*EXV2PH/ERVPH
         DETU=-EPST1/DERU*F2P*EXU2
      DETVTH=EPST1/DEPS*F2P*EXV2TH*(DEPSPH+2.D0*ERVPH*VLCTH**2)
      DETVPH=EPST1/DEPS*F2P*EXV2PH*(DEPSTH+2.D0*ERVTH*VLCPH**2)
C
          D0DER1=DERUU*D0U+D0VTH*DERVTH+D0VPH*DERVPH
          D1DER1=DERUU*D1U+D1VTH*DERVTH+D1VPH*DERVPH
          D2DER1=DERUU*D2U+D2VTH*DERVTH+D2VPH*DERVPH
          DTDER1=         DTVTH*DERVTH+DTVPH*DERVPH
C
          D0DET1=DETU*D0U+D0VTH*DETVTH+D0VPH*DETVPH
          D1DET1=DETU*D1U+D1VTH*DETVTH+D1VPH*DETVPH
          D2DET1=DETU*D2U+D2VTH*DETVTH+D2VPH*DETVPH
          DTDET1=         DTVTH*DETVTH+DTVPH*DETVPH
C
          END IF
C
          RETURN
          END
